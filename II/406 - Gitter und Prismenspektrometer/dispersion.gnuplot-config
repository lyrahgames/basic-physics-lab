set terminal epslatex size 15cm,9cm
set output "dispersion.tex"

set title 'Dispersionskurve $n(\lambda)$'

set xl '$\lambda \ [nm]$'
set yl '$n \ [1]$'

set format x '%.0f'
set format y '%.3f'

f(x) = a*x**2 + b*x + c
fit f(x) 'dispersion.gnuplot-data' using 9:7 via a,b,c

set label '$a = (6.5 \pm 1.0) \ 10^{-7} \ nm^{-2}$' at 550,1.65
set label '$b = (-8.5 \pm 1.1) \ 10^{-4} \ nm^{-1}$' at 550,1.645
set label '$c = (1.893 \pm 0.028)$' at 550,1.64

set key spacing 1.2

plot "dispersion.gnuplot-data" \
	using 9:7:10:8 \
	title 'Messwerte' \
	with xyerrorbars pt 13 ps 0.8, \
	\
	f(x) \
	title '$f(x)=ax^2+bx+c$'