set terminal epslatex size 15cm,9cm
set output "diagram2.tex"

set title 'Diagramm zur Bestimmung der Stoffmenge durch Extrapolation'
set xl '$1/V \ [cm^{-3}]$'
set yl '$pV \ [bar\cdot cm^3]$'

set format x '%.1f'
set format y '%.0f'

f(x) = a*x + b
set fit errorvariables
fit [0:1.5] f(x) 'isotherm.gnuplot-data' every :::3::3 \
	using (1/$3):($4/($3*$3)):($3*$5):($3*$5*($4/$3 + $6/$5)) via a,b

set label sprintf("$a \\pm \\Delta a = (%.2f \\pm %.2f) \\ bar\\cdot cm^6$", a, a_err) at 2.0,45
set label sprintf("$b \\pm \\Delta b = (%.2f \\pm %.2f) \\ bar\\cdot cm^3$", b, b_err) at 2.0,40

plot [0:4] [10:55]'isotherm.gnuplot-data' every :::3::3 \
	using (1/$3):($3*$5):($4/($3*$3)):($3*$5*($4/$3 + $6/$5)) \
	title 'Messwerte $\vartheta = 45.5 ^\circ C$' \
	with xyerrorbars pt 13 ps 0.8, \
	\
	f(x) title '$f(x) = ax+b$ im Intervall $[0,1.5]$'
