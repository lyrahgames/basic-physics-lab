set terminal epslatex size 15cm,9cm
set output "pD-T-diagram.tex"

set title 'Dampfdruckkurve $p_D(\vartheta)$ ohne Korrektur'
set xl '$\vartheta \ [^\circ C]$'
set yl '$p_D \ [Torr]$'

set key left top
set key spacing 5

set fit errorvariables
f(x) = a*exp(-b/(x+273.15))
fit f(x) 'pD-T-diagram.gnuplot-data' using 1:6 via a,b

set label sprintf("$a\\pm \\Delta a = (%.1f \\pm %.1f) \\ 10^{8} \\ Torr$", a*1e-8, a_err*1e-8) at 30,700

set label sprintf("$b\\pm \\Delta b = (%.0f \\pm %.0f) \\ K$", b, b_err) at 30,600

plot 'pD-T-diagram.gnuplot-data' \
	using 1:6:2:7 \
	title 'Messwerte ohne Korrektur' \
	with xyerrorbars pt 13 ps 0.8, \
	\
	f(x) title '$f(x) = a \exp \left(\dfrac{-b}{x+273.15 \ K}\right )$'